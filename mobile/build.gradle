// Main build script for Stocks
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK
//        gradlew test                    Run local unit test
//        gradlew connectedAndroidTest    Run instrumented unit test

final VERSION_MAJOR = 1 // max two digits
final VERSION_MINOR = 0 // max two digits
final VERSION_PATCH = 0 // max two digits
final VERSION_BUILD = 0 // max three digits

apply plugin: "com.android.application"
apply from: "../utils.gradle"

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.3"

	defaultConfig {
		applicationId "com.example.stocks"
		minSdkVersion 19
		targetSdkVersion 29
		versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH, VERSION_BUILD)
		versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		release {
			def keystoreProperties = getKeystoreProperties file("../${project.property('keystore.properties')}")
			storeFile file("../${project.property('keystore.file')}")
			storePassword keystoreProperties["keystore.store.password"]
			keyAlias keystoreProperties["keystore.key.alias"]
			keyPassword keystoreProperties["keystore.key.password"]
		}
	}

	buildTypes {
		debug {
			buildConfigField "boolean", "LOGS", "true"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "true"

			versionNameSuffix "-debug"
		}

		release {
			buildConfigField "boolean", "LOGS", "false"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "false"

			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

	buildFeatures {
		dataBinding true
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	setProperty("archivesBaseName", getArtifactName(defaultConfig))
}

dependencies {
	implementation fileTree(dir: "libs", include: ["*.jar"])
	implementation "androidx.annotation:annotation:1.1.0"
	implementation "androidx.appcompat:appcompat:1.1.0"
	implementation "androidx.constraintlayout:constraintlayout:1.1.3"
	implementation "androidx.fragment:fragment:1.2.5"
	implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
	implementation "androidx.lifecycle:lifecycle-runtime:2.2.0"
	implementation "androidx.recyclerview:recyclerview:1.1.0"
	implementation "androidx.viewpager:viewpager:1.0.0"
	implementation "com.github.bumptech.glide:glide:3.8.0"
	implementation "com.github.bumptech.glide:okhttp3-integration:1.5.0@aar"
	implementation "com.google.android.material:material:1.1.0"
	implementation "com.google.code.gson:gson:2.8.6"
	implementation "com.squareup.leakcanary:leakcanary-object-watcher-android:2.4"
	debugImplementation "com.squareup.leakcanary:leakcanary-android:2.4"
	implementation "com.squareup.okhttp3:logging-interceptor:4.8.0"
	implementation "com.squareup.okhttp3:okhttp:4.8.0"
	implementation "com.squareup.retrofit2:adapter-rxjava3:2.9.0"
	implementation "com.squareup.retrofit2:converter-gson:2.9.0"
	implementation "com.squareup.retrofit2:retrofit:2.9.0"
	implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
	implementation "io.reactivex.rxjava3:rxjava:3.0.4"
	implementation "org.alfonz:alfonz-adapter:0.9.2"
	implementation "org.alfonz:alfonz-arch:0.9.2"
	implementation "org.alfonz:alfonz-graphics:0.9.2"
	implementation "org.alfonz:alfonz-rest:0.9.2"
	implementation "org.alfonz:alfonz-rx:0.9.2"
	implementation "org.alfonz:alfonz-utility:0.9.2"
	implementation "org.alfonz:alfonz-view:0.9.2"
	testImplementation "junit:junit:4.13"
	testImplementation "org.mockito:mockito-core:3.4.4"
}
